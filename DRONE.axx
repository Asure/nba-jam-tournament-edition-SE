	.file	"drone.asm"
	.title	"basketball drone code"
	.width	132
	.option	b,d,l,t
	.mnolist


	.include	"mproc.equ"		;Mproc equates
	.include	"disp.equ"		;Display proc equates
	.include	"gsp.equ"		;Gsp asm equates
	.include	"sys.equ"
	.include	"audit.equ"
	.include	"shawn.hdr"		;Macros
	.asg		0,SEQT
	.include	"plyr.equ"





	.ref	plyrobj_t,plyrproc_t
	.ref	ballobj_p
	.ref	ballpnum
	.ref	seekdirdist_obxz128

	.ref	drone2on
	.ref	drone2_main,drone2_adjskill,drone2_setuptob

	.ref	game_time,gmqrtr
	.ref	shotimer

	.ref	team1,team2

	.ref	RNDPER
	.ref	PSTATUS
	.ref	GET_ADJ





	.bss	drnzzcnt	,16	;Drone zigzag mode cntdn
	.bss	drnzzmode	,16	;Drone zigzag mode (0-?)





	.text




 SUBR	drone_main

	move	@drone2on,b0
	jrnz	drone2_main


	move	@ballpnum,a14
	jrn	drone_chaseball		;No owner?

	move	*a13(plyr_ownball),a1
	jrz	drone_defence		;We don't have ball?
	jrn	drone_offwoball		;Teammate has ball?



	move	@PSTATUS,a0		;Plyr start bits 0-3
	move	*a13(plyr_num),a1
	XORK	1,a1
	btst	a1,a0
	jrz	iuretmdrone		;Teammate is a drone?

	move	*a13(plyr_d_cflgs),a2
	btst	DRN_PASS_B,a2
	jrz	iurenopass

	movi	BUT2_M<<8+BUT2_M,a14	;>Make him pass
	jruc	iuredocmd

iurenopass
	btst	DRN_SHOOT_B,a2
	jrz	iurenoshoot
	movi	BUT1_M|BUT3_M,a14	;>Make him shoot
	movk	3,a0
	move	a0,*a13(plyr_d_seekcnt)
iuredocmd
	move	*a11,a0
	sll	32-4,a0
	srl	32-4,a0
	ori	BUT3_M,a0		;+turbo
iurenoturb
	or	a14,a0
	move	a0,*a11
	clr	a0
	move	a0,*a13(plyr_d_cflgs)
	jruc	iurex

iurenoshoot
iuretmdrone

	movk	1,a0
	move	a0,*a13(plyr_d_mode)


	move	*a13(plyr_seqflgs),a2
	btst	PASS_B,a2
	jrnz	iurekilbuts

	move	*a13(plyr_jmpcnt),a14
	jrnz	iureinjmp

	move	*a11,a0
	btst	BUT1_B,a0
	jrnz	iurefake			;Shoot button down?


	btst	SHOOT_B,a2
	jrnz	iureinjmp


	move	*a13(plyr_dribmode),a14
	jrn	iurenodrib

	move	*a13(plyr_num),a14	;0CHhk for breakaway
	srl	1,a14
	movk	1,a0
	xor	a0,a14
	sll	6,a14			;*64
	addi	plyrproc_t,a14
	move	*a14+,a2,L
	move	*a14+,a3,L

	move	*a2(plyr_hpdist),a2
	move	*a3(plyr_hpdist),a3
	move	*a13(plyr_ohpdist),a4

	cmp	a4,a2
	jrlt	iureshootrnd		;He's closer?
	cmp	a4,a3
	jrlt	iureshootrnd		;He's closer?

	move	*a13(plyr_ohoopx),a0
	movi	CZMID,a1
	callr	drone_seekxy
	move	*a13(plyr_d_skill),a14
	cmpi	-11-3,a14
	jrle	iurenoturb2
	ori	BUT3_M,a0		;Push turbo
	move	a0,*a11
iurenoturb2

	move	@game_time,a1,L
	cmpi	0200H,a1
	jrlt	iureshoot2			;Less than 2 secs?
	move	@shotimer+16,a1		;Tens
	jrnz	iurescok
	move	@shotimer,a1		;Ones
	subk	2,a1
	jrlt	iureshoot2			;Less than 2 secs?
iurescok
	move	*a13(plyr_ohpdist),a14
	cmpi	170,a14
	jrge	iurex			;Too far?
	cmpi	50,a14
	jrlt	iureshoot2			;Close?
	movk	01fH,a0
	callr	rnd
	jrnz	iurex
	jruc	iureshoot2


iurenodrib					;0CaHn't dribble
	move	*a13(plyr_ohpdist),a0
	cmpi	230,a0
	jrlt	iureshoot2
	callr	drone_pass
	jrnz	iurex			;Pass OK?
	movk	01fH,a0
	callr	rnd
	jrnz	iurex			;97%?
	jruc	iureshoot2


iureshootrnd
	move	@game_time,a1,L
	cmpi	0200H,a1
	jrlt	iureshoot2			;Less than 2 secs?
	move	@shotimer+16,a1
	jrnz	iurescok2
	move	@shotimer,a1
	subk	2,a1
	jrlt	iureshoot2			;Less than 2 secs?
iurescok2
	PUSH	a6,a7
	move	*a13(plyr_ohoopx),a6
	movi	CZMID,a7
	calla	seekdirdist_obxz128
	PULL	a6,a7

	move	*a13(plyr_o1dist),a14
	cmpi	80,a14
	jrgt	iureo1dok			;He's too far?
	cmp	a14,a1
	jrlt	iureo1dok			;I'm closer?
	move	*a13(plyr_o1dir),a2
	sub	a0,a2
	abs	a2
	cmpi	040H,a2
	jrle	iureo1dsml
	subi	080H,a2
	abs	a2
iureo1dsml	subk	32,a2
	jrlt	iuregoaround		;In front of me?
iureo1dok
	move	*a13(plyr_o2dist),a14
	cmpi	80,a14
	jrgt	iureo2dok			;He's too far?
	cmp	a14,a1
	jrlt	iureo2dok			;I'm closer?
	move	*a13(plyr_o2dir),a2
	sub	a0,a2
	abs	a2
	cmpi	040H,a2
	jrle	iureo2dsml
	subi	080H,a2
	abs	a2
iureo2dsml	subk	32,a2
	jrlt	iuregoaround		;In front of me?
iureo2dok
	jruc	iurerunath

iuregoaround				;>Opponent in my way
	move	*a13(plyr_tmdist),a0
	cmpi	70,a0
	jrlt	iuregoa			;Teammate too close?

	callr	drone_pass
	jrnz	iurex			;Pass OK?

iuregoa
	movi	drnzzcnt,a2
	move	*a2,a0
	subk	1,a0
	jrgt	iurezzsame

	movk	1,a0			;New mode
	callr	rnd
	move	a0,*a2(drnzzmode-drnzzcnt)
	movi	TSEC-10,a0
	callr	rndrng0
	addk	10,a0
iurezzsame
	move	a0,*a2

	move	*a13(plyr_ohoopx),a0
	movi	CZMID,a1
	callr	drone_seekxy
	jrz	iureshoot2			;In position?
	sll	3,a0			;*8
	addi	iurejbits_t,a0

	move	*a2(drnzzmode-drnzzcnt),a14
	jrz	iuremd1
	addi	16*8,a0
iuremd1	movb	*a0,a0
	move	a0,*a11

	cmpi	80,a4
	jrlt	iureshoot2			;Close enough for jam?

	jruc	iuretryshot


iurerunath					;>I have a clr path to hoop!
	move	*a13(plyr_o1dist),a14
	cmpi	65,a14
	jrlt	iuregoa			;He's too close?
	move	*a13(plyr_o2dist),a14
	cmpi	65,a14
	jrlt	iuregoa			;He's too close?

	move	*a13(plyr_ohoopx),a0
	movi	CZMID,a1
	callr	drone_seekxy
	move	*a13(plyr_d_skill),a14
	cmpi	-11-3,a14
	jrle	iuretryshot
	ori	BUT3_M,a0		;Turbo
	move	a0,*a11


iuretryshot
	cmpi	50,a4
	jrlt	iureshoot2			;Close enough for jam?

	move	@PSTATUS,a0		;Plyr start bits 0-3
	move	*a13(plyr_num),a1
	XORK	1,a1
	btst	a1,a0
	jrnz	iurex			;Teammate is a human?

	cmpi	230,a4			;0CHhk if we need 3ptrs
	jrlt	iureno3
	cmpi	290,a4
	jrgt	iureno3

	movk	6,a1
	move	@game_time,a2,L
	cmpi	01010000H,a2
	jrgt	iurehvtime			;Enough time?
	movk	3,a1
iurehvtime
	move	*a13(plyr_ptsdown),a14
	cmp	a1,a14
	jrlt	iureno3

	cmpi	040000H,a2
	jrlt	iureshoot2			;Less than 40 secs?

	movk	8,a0
	move	*a13(plyr_o1dist),a14
	cmpi	70,a14
	jrlt	iurerndsht			;He's close?
	move	*a13(plyr_o2dist),a14
	cmpi	70,a14
	jrlt	iurerndsht			;He's close?
	jruc	iureshoot2

iureno3
	movi	50,a0
	move	*a13(plyr_d_skill),a14
	cmpi	-8,a14
	jrgt	iurerndsht
	movk	30,a0
iurerndsht	callr	rndrng0
	move	a0,a0
	jrnz	iurex

iureshoot
	move	*a13(plyr_ohpdist),a14
	cmpi	240,a14
	jrge	iurex			;Too far?

iureshoot2	move	*a11,a0			;>Shoot
	ori	BUT1_M|BUT3_M,a0
	move	a0,*a11
	movk	1,a0			;Max fakes
	move	a0,*a13(plyr_d_seekcnt)
	jruc	iurex



iurefake					;>Jumping but still on gnd
	move	*a13(plyr_d_seekcnt),a2
	jrle	iurex			;No fakes?
	move	@PSTATUS,a0		;Plyr start bits 0-3
	move	*a13(plyr_num),a1
	XORK	1,a1
	btst	a1,a0
	jrnz	iurex			;Teammate is a human?
	movk	01fH,a0
	callr	rnd
	jrnz	iurex

	move	@game_time,a1,L
	cmpi	0200H,a1
	jrlt	iurex			;Less than 2 secs?
	move	@shotimer+16,a1		;Tens
	jrnz	iurefk
	move	@shotimer,a1		;Ones
	subk	2,a1
	jrlt	iurex			;Less than 2 secs?
iurefk
	subk	1,a2
	move	a2,*a13(plyr_d_seekcnt)
	jruc	iurekilbuts


iureinjmp
	move	@PSTATUS,a0		;Plyr start bits 0-3
	move	*a13(plyr_num),a1
	XORK	1,a1
	btst	a1,a0
	jrz	iuretmdrn			;Teammate is a drone?

	move	*a13(plyr_tmproc_p),a0,L
	move	*a0(PA11),a0,L
	move	*a0,a0			;Get teammates ctrl bits
	btst	BUT1_B,a0
	jrnz	iurex			;Holding shoot button?
	jruc	iurekilbuts
iuretmdrn
	move	*a3(plyr_seqflgs),a0
	btst	BLOCKREB_B,a0
	jrnz	iurekilbuts		;Got a rebound?

	movk	15,a0
	callr	rndrng0
	move	a0,a0
	jrz	iurekilbuts		;Cause shoot?

	movk	7,a0
	callr	rnd
	jrnz	iurex			;88%?

	move	*a13(plyr_ptsdown),a14
	addk	5,a14
	jrlt	iurepss			;Winning by 05H?
	move	*a13(plyr_seqflgs),a14
	btst	DUNK_B,a14
	jrnz	iurex			;In a dunk?
	move	@game_time,a1,L
	cmpi	0200H,a1
	jrlt	iurex			;Less than 2 secs?

iurepss	callr	drone_pass

	jruc	iurex



iurekilbuts
	clr	a0			;>Let go of shoot button
	move	a0,*a11


iurex	rets


iurejbits_t
	.byte	0,JOYL_M,JOYR_M,0
	.byte	JOYD_M,JOYD_M|JOYL_M,JOYD_M|JOYR_M,0
	.byte	JOYU_M,JOYU_M|JOYL_M,JOYU_M|JOYR_M,0
	.byte	0,0,0,0

	.byte	0,JOYR_M,JOYL_M,0
	.byte	JOYU_M,JOYU_M|JOYR_M,JOYU_M|JOYL_M,0
	.byte	JOYD_M,JOYD_M|JOYR_M,JOYD_M|JOYL_M,0
	.byte	0,0,0,0




 SUBRP	drone_offwoball

	clr	a0
	move	a0,*a13(plyr_d_cflgs)


	move	*a13(plyr_d_mode),a14
	jrgt	xiylinmd			;Already in mode?

	movk	1,a0
	move	a0,*a13(plyr_d_mode)

	callr	drone_getcurskillo
	addi	xiylmdsk_t,a0
	move	*a0,a0
	callr	rndrng0
	addk	1,a0
	move	a0,*a13(plyr_d_seekcnt)
xiylinmd


	move	*a13(plyr_dir),a3

	move	*a13(plyr_o1dist),a14
	subi	50,a14
	jrgt	xiylo1far				;Too far?
	move	*a13(plyr_o1dir),a2
	sub	a3,a2
	abs	a2
	cmpi	040H,a2
	jrle	xiylo1dsml
	subi	080H,a2
	abs	a2
xiylo1dsml	subk	16,a2
	jrlt	xiylpusho				;In front of me?
xiylo1far
	move	*a13(plyr_o2dist),a14
	subi	50,a14
	jrgt	xiylnopush				;Too far?
	move	*a13(plyr_o2dir),a2
	sub	a3,a2
	abs	a2
	cmpi	040H,a2
	jrle	xiylo2dsml
	subi	080H,a2
	abs	a2
xiylo2dsml	subk	16,a2
	jrge	xiylnopush				;!In front?
xiylpusho
	movi	99,a0
	callr	rndrng0

	move	*a13(plyr_ptsdown),a14
	move	*a13(plyr_d_skill),a1
	add	a1,a14
	cmpi	15,a14
	jrle	xiylmxdnok
	movk	15,a14
xiylmxdnok	addk	15,a14
	jrge	xiyldnok
	clr	a14
xiyldnok	sll	4,a14
	addi	xiylp_t,a14
	move	*a14,a1

	cmp	a1,a0
	jrge	xiylnewseek			;Skip push?

	move	*a11,a0				;Push
	ori	BUT2_M<<8|BUT2_M|BUT3_M,a0
	move	a0,*a11
	jruc	xiylx
xiylnopush


	movi	07fH,a0
	callr	rnd
	jrz	xiylnewseek

	move	*a13(plyr_d_seekcnt),a0
	subk	1,a0
	jrgt	xiylseek

xiylnewseek
	move	*a13(plyr_newdir),a0
	jrnn	xiylcontsk				;Turning?

	movk	16,a0
	callr	rndrng0
	sll	5,a0				;*32
	addi	xiylseek_t,a0

	move	*a0+,a1
	move	*a13(plyr_num),a14
	subk	2,a14
	jrge	xiyllft
	neg	a1
xiyllft	addi	WRLDMID,a1
	move	a1,*a13(plyr_d_seekx)

	move	*a0+,a1
	move	a1,*a13(plyr_d_seeky)

	movi	TSEC*3,a0
	callr	rndrng0
	addk	TSEC/2,a0

xiylseek	move	a0,*a13(plyr_d_seekcnt)
xiylcontsk
	move	*a13(plyr_d_seekx),a0
	move	*a13(plyr_d_seeky),a1
	callr	drone_seekxy
	jrnz	xiylnotthere

	movk	01fH,a0				;3%
	callr	rnd
	jrnz	xiylx
	clr	a0
	move	a0,*a13(plyr_d_seekcnt)

xiylnotthere


xiylx
	rets


xiylmdsk_t					;Mode switch max seek time
	.word	50,50,50,50,50		;Up 15-11
	.word	40,40,40,35,30		;10-6
	.word	25,22,20,18,16		;5-1
	.word	14			;Even score
	.word	10,8,6,4,4		;Dn 1-5
	.word	4,3,3,3,2		;6-10
	.word	2,2,2,2,1		;11-15

xiylp_t					;% to push
	.word	1,1,1,1,1
	.word	2,2,2,2,3
	.word	3,3,4,4,5
	.word	5
	.word	5,6,8,10,13
	.word	15,17,18,20,20
	.word	25,30,35,40,50


	.asg	CZMID,Z
xiylseek_t
	.word	-320,Z-160, -250,Z-160, -120,Z-100	;3ptrs
	.word	-120,Z
	.word	-120,Z+100, -250,Z+160, -320,Z+160

	.word	-320,Z-100, -250,Z-80, -200,Z-50	;2ptrs
	.word	-180,Z
	.word	-200,Z+50, -250,Z+80, -320,Z+100

	.word	-310,Z-10, -305,Z, -310,Z+10



 SUBRP	drone_pass

	move	@PSTATUS,a0		;Plyr start bits 0-3
	move	*a13(plyr_num),a1
	XORK	1,a1
	btst	a1,a0
	jrnz	vwuyx			;Teammate is a human?

	move	*a13(plyr_tmdist),a0
	addk	10,a0
	move	*a13(plyr_o1dist),a1
	cmp	a1,a0
	jrlt	vwuyo1ok

	move	*a13(plyr_tmdir),a14
	move	*a13(plyr_o1dir),a1
	sub	a14,a1
	abs	a1
	cmpi	64,a1
	jrle	vwuydsml
	subi	128,a1
	abs	a1
vwuydsml	subk	16,a1
	jrlt	vwuyx

vwuyo1ok
	move	*a13(plyr_o2dist),a1
	cmp	a1,a0
	jrlt	vwuyo2ok

	move	*a13(plyr_tmdir),a14
	move	*a13(plyr_o2dir),a1
	sub	a14,a1
	abs	a1
	cmpi	64,a1
	jrle	vwuydsml2
	subi	128,a1
	abs	a1
vwuydsml2	subk	16,a1
	jrlt	vwuyx

vwuyo2ok
	move	*a13(plyr_tmproc_p),a1,L
	move	*a1(plyr_seq),a0
	subk	RUNDRIBTURB_SEQ,a0
	jrhi	vwuyx

	move	*a11,a0			;>Pass
	ori	BUT2_M<<8|BUT2_M|BUT3_M,a0
	move	a0,*a11

	rets

vwuyx	clr	a0
	rets



 SUBRP	drone_defence

	PUSH	a7


	move	@ballpnum,a5
	sll	5,a5
	addi	plyrproc_t,a5
	move	*a5,a5,L		;A5=*Proc of opponent with ball


	callr	drone_getcurskillo
	move	a0,a7			;A7=Ptsdn+skill for indexing (*16)


	clr	a0
	move	a0,*a13(plyr_d_cflgs)

	move	*a13(plyr_d_mode),a14
	jrn	aclvinmd			;Already in mode?

	subk	1,a0			;-1
	move	a0,*a13(plyr_d_mode)

	move	a7,a14
	addi	aclvmdsk_t,a14
	move	*a14,a0
	move	a0,a2
	srl	2,a2			;/4

	callr	rndrng0
	addk	1,a0
	add	a2,a0
	move	a0,*a13(plyr_d_seekcnt)
	
aclvinmd



	move	*a13(plyr_d_seekcnt),a0
	subk	1,a0
	jrgt	aclvseek

	move	*a13(plyr_num),a2
	XORK	2,a2
	move	a2,a4
	sll	5,a4			;*32
	addi	plyrproc_t,a4
	move	*a4,a4,L
	cmp	a5,a4
	jreq	aclvguard			;I'm on guy with ball?

	move	*a5(plyr_ohpdist),a0
	cmpi	300,a0
	jrgt	aclvguard			;Too far to worry about?

	move	*a5(plyr_seqflgs),a0
	btst	DUNK_B,a0
	jrnz	aclvgbc			;He's dunking?

	move	*a13(plyr_tmproc_p),a3,L
	move	*a3(plyr_seq),a0
	subi	STAGGER_SEQ,a0
	jrls	aclvtmok
	subk	FLYBACKWB2_SEQ-STAGGER_SEQ,a0
	jrls	aclvgbc			;Teammates staggered?
aclvtmok
	move	*a3(plyr_o1dist),a14
	move	*a3(plyr_o1dir),a1
	btst	0,a2
	jrnz	aclvp1
	move	*a3(plyr_o2dist),a14
	move	*a3(plyr_o2dir),a1
aclvp1
	move	*a3(plyr_hpdir),a0	;Find dir difference
	sub	a1,a0
	abs	a0
	cmpi	040H,a0
	jrle	aclvdsml
	subi	080H,a0
	abs	a0
aclvdsml	subk	28,a0
	jrle	aclvgbc			;TM not between op and hoop?

	cmpi	160,a14
	jrgt	aclvgbc			;TM not guarding?
	jruc	aclvguard

aclvgbc	move	a5,a4			;Guard ball carrier
aclvguard
	move	*a4(PA8),a2,L		;*Obj

	move	*a2(OXPOS),a0
	move	*a2(OXANI+16),a14
	add	a14,a0			;X
	move	*a2(OZPOS),a1		;Z

	move	@game_time,a14,L
	cmpi	0800H,a14
	jrlt	aclvsetseek		;Less than 8 secs?
	move	@shotimer+16,a14	;Tens
	jrnz	aclvscok
	move	@shotimer,a14		;Ones
	subk	5,a14
	jrlt	aclvsetseek		;Less than 5 secs?
aclvscok
	move	*a5(plyr_seqflgs),a14
	btst	SHOOT_B,a14
	jrnz	aclvsetseek		;Starting a shot?

	move	*a2(OXVEL),a14,L
	sra	16-4,a14		;16 ticks from now
	add	a14,a0
	move	*a2(OZVEL),a14,L
	sra	16-4,a14		;16 ticks from now
	add	a14,a1

	move	*a13(plyr_myhoopx),a2	;>Stay near oplyr between my basket
	movi	CZMID,a3

	add	a0,a2
	sll	1,a0
	add	a2,a0
	sra	2,a0			;/4

	add	a1,a3
	sll	1,a1
	add	a3,a1
	sra	2,a1			;/4
aclvsetseek
	move	a0,*a13(plyr_d_seekx)
	move	a1,*a13(plyr_d_seeky)

	movk	25,a0
	move	*a5(plyr_hpdist),a1
	cmpi	310,a1
	jrge	aclvseek			;Far from my hoop?


	move	a7,a14
	addi	aclvskt_t,a14
	move	*a14,a0
	move	a0,a2
	srl	2,a2			;/4
	callr	rndrng0
	add	a2,a0

aclvseek	move	a0,*a13(plyr_d_seekcnt)

	move	*a13(plyr_d_seekx),a0
	move	*a13(plyr_d_seeky),a1
	callr	drone_seekxy
	move	a0,a2



	move	*a13(plyr_num),a14	;>Get opponents proc
	addk	2,a14
	sll	32-2,a14
	srl	32-2-5,a14		;*32
	addi	plyrproc_t,a14
	move	*a14,a0,L
	move	*a0(plyr_ohpdist),a0
	subk	10,a0
	cmpi	300,a0
	jrlt	aclvlong
	subi	80,a0			;Turbo earlier
aclvlong
	move	*a13(plyr_hpdist),a1
	cmp	a0,a1
	jrlt	aclvicloser
	addi	BUT3_M,a2		;Turbo
aclvicloser


	move	*a13(plyr_balldist),a1
	cmpi	30,a1
	jrgt	aclvpsrnd			;!In his face?

	move	@game_time,a14,L
	cmpi	0800H,a14
	jrlt	aclvps			;Less than 8 secs?
	move	@shotimer+16,a14	;Tens
	jrnz	aclvpsrnd
	move	@shotimer,a14		;Ones
	subk	5,a14
	jrlt	aclvps			;Less than 5 secs?
aclvpsrnd
	move	a7,a14
	addi	aclvdist_t,a14
	move	*a14,a0
	cmp	a0,a1
	jrgt	aclvskipsp			;Ball too far?

	movi	999,a0
	callr	rndrng0

	move	a7,a14
	addi	aclvps_t,a14
	move	*a14,a1
	cmp	a1,a0
	jrge	aclvskipsp			;Skip push?
aclvps
	sll	32-4,a2
	srl	32-4,a2
	addk	BUT2_M,a2		;Steal

	movk	1,a0
	callr	rnd
	jrnz	aclvskipsp			;50%?

	addi	BUT2_M<<8|BUT3_M,a2	;Push
aclvskipsp


	move	*a13(plyr_balldist),a14	;0CHhk if I can block ball
	cmpi	65,a14
	jrge	aclvnoblk

	move	*a5(plyr_jmpcnt),a0
	jrnz	aclvtryblk			;Plyr with ball is in air?

	move	*a5(plyr_seqflgs),a0
	btst	SHOOT_B,a0
	jrz	aclvnoblk			;!Starting a shot?
	movk	11,a0
	callr	rndrng0
	move	a0,a0
	jrnz	aclvnoblk			;94% ignore?
	jruc	aclvblk

aclvtryblk
	movi	99,a0
	callr	rndrng0
	move	a7,a14
	addi	aclvblk_t,a14
	move	*a14,a1
	cmp	a1,a0
	jrge	aclvnoblk

aclvblk	ORK	BUT1_M,a2		;Block
aclvnoblk
	move	a2,*a11

	PULL	a7
	rets


aclvmdsk_t					;Mode switch max seek time
	.word	50,50,50,50,50		;Up 15-11
	.word	50,50,45,45,40		;10-6
	.word	33,25,22,18,16		;5-1
	.word	14			;Even score
	.word	10,8,6,4,4		;Dn 1-5
	.word	4,3,3,3,2		;6-10
	.word	2,2,2,2,1		;11-15

aclvskt_t					;Max seek time
	.word	60,60,58,55,52
	.word	48,44,40,36,32
	.word	28,24,22,18,16
	.word	14			;Even score
	.word	10,8,6,4,4
	.word	4,3,3,3,2
	.word	2,2,2,2,1

aclvdist_t					;Max dist to try push/steal
	.word	110,100,100,100,100
	.word	90,90,80,80,80,80
	.word	70,70,60,60,60
	.word	50
	.word	50,50,50,50,50
	.word	50,50,50,50,50
	.word	50,50,50,50,50
aclvps_t					;% to push/steal
	.word	2,2,2,2,2
	.word	3,3,3,3,3
	.word	4,4,4,5,5
	.word	6
	.word	6,6,8,10,13
	.word	15,17,18,20,30
	.word	40,60,80,150,250
aclvblk_t					;% to block
	.word	1,1,2,3,3
	.word	3,3,4,4,5
	.word	6,7,8,10,12
	.word	14
	.word	16,18,20,25,30
	.word	35,40,45,50,50
	.word	50,50,50,50,50



 SUBR	drone_setuptob

	move	@drone2on,b0
	jrnz	drone2_setuptob

	PUSH	a2,a3,a4,a13

	movk	4,a4
	movi	plyrproc_t,a3

msuwlp	move	*a3+,a13,L
	move	*a13(plyr_ownball),a14
	jrnz	msuwnxt			;Offense?

	movi	-1,a14
	move	a14,*a13(plyr_d_mode)	;Defense

	callr	drone_getcurskillo

	addi	msuwmdsk_t,a0
	move	*a0,a0
	move	a0,a2
	srl	2,a2			;/4

	callr	rndrng0
	addk	1,a0
	add	a0,a2

	movk	1,a0
	callr	rnd
	jrnz	msuwsk
	srl	1,a2
msuwsk
	move	a2,*a13(plyr_d_seekcnt)

msuwnxt
	dsj	a4,msuwlp

	PULL	a2,a3,a4,a13
	rets

msuwmdsk_t					;Mode switch max seek time
	.word	50,50,50,50,50		;Up 15-11
	.word	50,50,45,45,40		;10-6
	.word	33,25,22,18,16		;5-1
	.word	14			;Even score
	.word	10,8,6,4,4		;Dn 1-5
	.word	4,3,3,3,2		;6-10
	.word	2,2,2,2,1		;11-15



 SUBRP	drone_chaseball

	move	*a13(plyr_rcvpass),a14
	jrgt	drone_offwoball		;I'm rcving ball?

	move	*a13(plyr_tmproc_p),a2,L
	move	*a2(plyr_rcvpass),a14
	jrgt	drone_offwoball		;Teammate rcving ball?


	clr	a0
	move	a0,*a13(plyr_d_cflgs)

	move	*a13(plyr_d_mode),a14
	jrz	cemoinmd			;Already in mode?

	move	a0,*a13(plyr_d_mode)

	move	*a13(plyr_ptsdown),a14
	move	*a13(plyr_d_skill),a0
	add	a0,a14
	cmpi	15,a14
	jrle	cemomxdnok
	movk	15,a14
cemomxdnok	addk	15,a14
	jrge	cemodnok
	clr	a14
cemodnok	sll	4,a14
	addi	cemomdsk_t,a14
	move	*a14,a0
	callr	rndrng0
	addk	2,a0
	move	a0,*a13(plyr_d_seekcnt)

cemoinmd

	move	*a13(plyr_d_seekcnt),a0
	subk	1,a0
	jrgt	cemoseek

	move	*a2(plyr_seq),a0
	subi	STAGGER_SEQ,a0
	jrls	cemotmok
	subk	FLYBACKWB2_SEQ-STAGGER_SEQ,a0
	jrls	cemochaseb			;Teammates staggered?
cemotmok
	move	*a13(plyr_balldist),a0
	move	*a2(plyr_balldist),a14
	cmp	a14,a0
	jrlt	cemochaseb			;I'm closer?

	movi	WRLDMID,a0
	movi	CZMID,a1
	jruc	cemosetxz

cemochaseb
	move	@ballobj_p,a5,L

	move	*a5(OXPOS),a0
	move	*a5(OXANI+16),a14
	add	a14,a0
	move	*a5(OXVEL),a14,L
	sra	16-3,a14		;8 ticks from now
	add	a14,a0

	move	*a5(OZPOS),a1
	move	*a5(OZVEL),a14,L
	sra	16-3,a14		;8 ticks from now
	add	a14,a1
cemosetxz
	move	a0,*a13(plyr_d_seekx)
	move	a1,*a13(plyr_d_seeky)


	move	*a13(plyr_ptsdown),a14
	move	*a13(plyr_d_skill),a0
	add	a0,a14
	cmpi	15,a14
	jrle	cemomxdnok2
	movk	15,a14
cemomxdnok2
	addk	15,a14
	jrge	cemodnok2
	clr	a14
cemodnok2	sll	4,a14
	addi	cemoskt_t,a14
	move	*a14,a0

	callr	rndrng0
	addk	5,a0
cemoseek	move	a0,*a13(plyr_d_seekcnt)

	move	*a13(plyr_d_seekx),a0
	move	*a13(plyr_d_seeky),a1
	callr	drone_seekxy


	move	*a13(plyr_balldist),a2	;0CHhk if I can jump at ball
	subk	30,a2
	jrge	cemonojmp

	move	*a5(OYPOS),a0
	addk	25,a0
	move	*a8(OYPOS),a1
	cmp	a1,a0
	jrlt	cemonojmp

	movk	7,a0
	callr	rnd
	jrnz	cemonojmp			;87%?

	move	*a11,a0			;Jmp
	ORK	BUT1_M,a14
	move	a0,*a11
cemonojmp

	rets

cemomdsk_t					;Mode switch max seek time
	.word	90,90,90,90,80		;Up 15-11
	.word	80,70,60,55,50		;10-6
	.word	33,25,22,18,16		;5-1
	.word	14			;Even score
	.word	10,8,6,4,4		;Dn 1-5
	.word	4,3,3,3,2		;6-10
	.word	2,2,2,2,1		;11-15

cemoskt_t					;Max seek time
	.word	50,50,45,45,45
	.word	40,40,30,30,20
	.word	20,16,14,13,12
	.word	12
	.word	12,11,10,10,9
	.word	8,5,4,3,2
	.word	1,1,1,1,1



 SUBRP	drone_seekxy

	move	a2,b0

	move	*a8(OXPOS),a2
	move	*a8(OXANI+16),a14
	add	a14,a2
	sub	a0,a2

	clr	a0

	move	a2,a14
	abs	a2
	subk	10,a2
	jrle	mqgxonx
	move	a14,a14
	jrlt	mqgxnolft
	subk	4,a0			;Left

mqgxnolft	addk	8,a0			;Rgt
mqgxonx
	move	*a8(OZPOS),a2

	sub	a1,a2
	move	a2,a14
	abs	a2
	subk	10,a2
	jrle	mqgxonz
	move	a14,a14
	jrlt	mqgxnoup
	subk	1,a0			;Up

mqgxnoup	addk	2,a0			;Dn
mqgxonz
	move	a0,*a11

	move	b0,a2
	move	a0,a0
	rets



 SUBRP	drone_getcurskillo

	move	*a13(plyr_ptsdown),a0
	move	*a13(plyr_d_skill),a14
	add	a14,a0
	cmpi	15,a0
	jrle	wsdamxdnok
	movk	15,a0
wsdamxdnok	addk	15,a0
	jrge	wsdadnok
	clr	a0
wsdadnok	sll	4,a0			;A7=Ptsdn+skill for indexing (*16)

	rets




 SUBR	drone_adjskill

	move	@drone2on,b0
	jrnz	drone2_adjskill


	PUSH	a2,a3,a4

	move	a0,a0
	jrnz	mrdlx

	movi	plyrproc_t,a4
	movk	4,b0
mrdllp
	move	*a4+,a3,L

	move	*a3(plyr_d_skill),a2

	move	*a3(plyr_ptsdown),a14
	cmpi	15,a14
	jrle	mrdlmxdnok
	movk	15,a14
mrdlmxdnok	addk	15,a14
	jrge	mrdldnok
	clr	a14
mrdldnok	sll	4,a14
	addi	mrdladj_t,a14
	move	*a14,a14
	add	a14,a2

	movk	ADJDIFF,a0		;Get difficulty level
	calla	GET_ADJ
	subk	3,a0			;-2 to 2
	sll	1,a0			;*2

	move	@gmqrtr,a14		;0CHhk quarter minimum
	cmpi	2,a14
	jrle	mrdlqok
	movk	2,a14
mrdlqok	sll	4,a14
	addi	mrdlmin_t,a14
	move	*a14,a14
	add	a0,a14
	cmp	a14,a2
	jrge	mrdlminok
	move	a14,a2
mrdlminok
	move	*a3(plyr_num),a1
	XORK	1,a1
	move	@PSTATUS,a14
	btst	a1,a14
	jrnz	mrdltminok			;Teammate is human?

	move	@team1,a14		;0CHhk team minimum
	cmpi	3,b0
	jrge	mrdlt1
	move	@team2,a14
mrdlt1	sll	3+2,a14

	move	@gmqrtr,a1
	cmpi	3,a1
	jrle	mrdlqok2
	movk	3,a1
mrdlqok2	sll	3,a1
	add	a1,a14

	addi	mrdltdmin_t-8,a14
	movb	*a14,a14
	add	a0,a14
	cmp	a14,a2
	jrge	mrdltminok
	move	a14,a2
mrdltminok
	move	a2,*a3(plyr_d_skill)

	dsj	b0,mrdllp

mrdlx	PULL	a2,a3,a4
	rets

mrdladj_t	.word	-5,-5,-5,-5,-5
	.word	-5,-5,-5,-4,-3
	.word	-2,-1,0,0,0
	.word	0
	.word	1,1,2,2,3
	.word	3,4,4,5,5
	.word	5,6,6,6,7

mrdlmin_t	.word	-8,-6,-4

mrdltdmin_t
	.byte	-8,-6,-4,0	;ATL
	.byte	-8,-6,-4,0	;BOST
	.byte	-8,-6,-4,0	;CHAR
	.byte	6,9,12,14	;CHI (1)
	.byte	-8,-6,-4,0	;CLEV
	.byte	-8,-6,-4,0	;DAL
	.byte	-8,-6,-4,0	;DEN
	.byte	-8,-6,-4,0	;DET
	.byte	-8,-6,-4,0	;GOLD
	.byte	-8,-6,-4,0	;HOU
	.byte	-8,-6,-4,0	;IND
	.byte	-8,-6,-4,0	;LAC
	.byte	-8,-6,-4,0	;LAL
	.byte	-8,-6,-4,0	;MIAMI
	.byte	-8,-6,-4,0	;MILW
	.byte	-8,-6,-4,0	;MINN
	.byte	-8,-6,-4,0	;NJ
	.byte	1,3,4,5		;NY (4)
	.byte	-8,-6,-4,0	;ORL
	.byte	-8,-6,-4,0	;PHIL
	.byte	4,6,7,8		;PHX (2)
	.byte	2,4,5,6		;PORT (3)
	.byte	-8,-6,-4,0	;SAC
	.byte	-8,-6,-4,0	;SANANT
	.byte	-8,-6,-4,0	;SEA
	.byte	0,2,3,4		;UTAH (5)
	.byte	-8,-6,-4,0	;WASH
	.even



 SUBRP	rnd

	move	@RAND,a1,L
	rl	a1,a1
	move	@HCOUNT,a14
	rl	a14,a1
	add	sp,a1
	move	a1,@RAND,L

	and	a1,a0
	rets



 SUBRP	rndrng0

	move	@RAND,a1,L
	rl	a1,a1
	move	@HCOUNT,a14
	rl	a14,a1
	add	sp,a1
	move	a1,@RAND,L

	addk	1,a0
	mpyu	a1,a0		;Condition codes not valid!

	rets




	.end

