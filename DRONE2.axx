	.file	"drone2.asm"
	.title	"basketball drone code"
	.width	132
	.option	b,d,l,t
	.mnolist


	.include	"mproc.equ"		;Mproc equates
	.include	"disp.equ"		;Display proc equates
	.include	"gsp.equ"		;Gsp asm equates
	.include	"sys.equ"
	.include	"audit.equ"
	.include	"shawn.hdr"		;Macros
	.asg		0,SEQT
	.include	"plyr.equ"





	.ref	plyrobj_t,plyrproc_t
	.ref	ballobj_p
	.ref	ballpnum,ballpnumshot
	.ref	plyrnumonfire
	.ref	seekdirdist_obxz128

	.ref	game_time,gmqrtr
	.ref	shotimer

	.ref	team1,team2

	.ref	RNDPER
	.ref	PSTATUS
	.ref	GET_ADJ





	.bss	drnzzcnt	,16	;Drone zigzag mode cntdn
	.bss	drnzzmode	,16	;Drone zigzag mode (0-?)
	BSSX	drone2on	,16	;!0=Use drone version2 code




	.text




 SUBR	drone2_main


	move	@ballpnum,a14
	jrn	drone_chaseball		;No owner?

	move	*a13(plyr_ownball),a1
	jrz	drone_defence		;We don't have ball?
	jrn	drone_offwoball		;Teammate has ball?



	move	@PSTATUS,a0		;Plyr start bits 0-3
	move	*a13(plyr_num),a1
	XORK	1,a1
	btst	a1,a0
	jrz	avbktmdrone		;Teammate is a drone?

	move	*a13(plyr_d_cflgs),a2
	btst	DRN_PASS_B,a2
	jrz	avbknopass

	movi	BUT2_M<<8+BUT2_M,a14	;>Make him pass
	jruc	avbkdocmd

avbknopass
	btst	DRN_SHOOT_B,a2
	jrz	avbknoshoot
	movi	BUT1_M|BUT3_M,a14	;>Make him shoot
avbkdocmd
	move	*a11,a0
	sll	32-4,a0
	srl	32-4,a0
	ori	BUT3_M,a0		;+turbo
avbknoturb
	or	a14,a0
	move	a0,*a11
	clr	a0
	move	a0,*a13(plyr_d_cflgs)
	jruc	avbkx

avbknoshoot
avbktmdrone

	move	*a13(plyr_d_mode),a14
	subk	2,a14
	jrge	avbkinmd			;Already in mode?

	movk	2,a1			;Offense with ball
	move	a1,*a13(plyr_d_mode)
	movk	1,a1
	jruc	avbksetskc
avbkinmd


	move	*a13(plyr_d_seekcnt),a1	;>Update tob mode
	jrle	avbknotob
avbksetskc	subk	1,a1
	move	a1,*a13(plyr_d_seekcnt)
	jrgt	avbknotob
	move	*a13(plyr_ohoopx),*a13(plyr_d_seekx)
	movi	CZMID,a0
	move	a0,*a13(plyr_d_seeky)
avbknotob

	move	*a13(plyr_seqflgs),a2
	btst	PASS_B,a2
	jrnz	avbkkilbuts

	move	*a13(plyr_jmpcnt),a14
	jrnz	avbkinjmp

	move	*a11,a0
	btst	BUT1_B,a0
	jrnz	avbkfake			;Shoot button down?


	btst	SHOOT_B,a2
	jrnz	avbkinjmp


	move	*a13(plyr_dribmode),a14
	jrn	avbknodrib

	move	*a13(plyr_num),a14	;0CHhk for breakaway
	srl	1,a14
	movk	1,a0
	xor	a0,a14
	sll	6,a14			;*64
	addi	plyrproc_t,a14
	move	*a14+,a2,L
	move	*a14+,a3,L

	move	*a2(plyr_hpdist),a2
	move	*a3(plyr_hpdist),a3
	move	*a13(plyr_ohpdist),a4

	cmp	a4,a2
	jrlt	avbkshootrnd		;He's closer?
	cmp	a4,a3
	jrlt	avbkshootrnd		;He's closer?

	move	*a13(plyr_ohoopx),a0
	movi	CZMID,a1
	callr	drone_seekxy
	move	*a13(plyr_d_skill),a14
	cmpi	-11-3,a14
	jrle	avbknoturb2
	ori	BUT3_M,a0		;Push turbo
	move	a0,*a11
avbknoturb2

	move	@game_time,a1,L
	cmpi	0200H,a1
	jrlt	avbkshoot2			;Less than 2 secs?
	move	@shotimer+16,a1		;Tens
	jrnz	avbkscok
	move	@shotimer,a1		;Ones
	subk	2,a1
	jrlt	avbkshoot2			;Less than 2 secs?
avbkscok
	callr	drone_chk3ptr
	jrnz	avbkshoot2			;Need a 3?

	move	*a13(plyr_ohpdist),a14
	cmpi	170,a14
	jrge	avbkx			;Too far?
	cmpi	50,a14
	jrlt	avbkshoot2			;Close?
	movk	01fH,a0
	callr	rnd
	jrnz	avbkx
	jruc	avbkshoot2


avbknodrib					;0CaHn't dribble
	move	*a13(plyr_ohpdist),a0
	cmpi	230,a0
	jrlt	avbkshoot2
	callr	drone_pass
	jrnz	avbkx			;Pass OK?
	movk	01fH,a0
	callr	rnd
	jrnz	avbkx			;97%?
	jruc	avbkshoot2


avbkshootrnd
	move	@game_time,a1,L
	cmpi	0200H,a1
	jrlt	avbkshoot2			;Less than 2 secs?
	move	@shotimer+16,a1
	jrnz	avbkscok2
	move	@shotimer,a1
	subk	2,a1
	jrlt	avbkshoot2			;Less than 2 secs?
avbkscok2
	PUSH	a6,a7
	move	*a13(plyr_ohoopx),a6
	movi	CZMID,a7
	calla	seekdirdist_obxz128
	PULL	a6,a7

	move	*a13(plyr_o1dist),a14
	cmpi	80,a14
	jrgt	avbko1dok			;He's too far?
	cmp	a14,a1
	jrlt	avbko1dok			;I'm closer?
	move	*a13(plyr_o1dir),a2
	sub	a0,a2
	abs	a2
	cmpi	040H,a2
	jrle	avbko1dsml
	subi	080H,a2
	abs	a2
avbko1dsml	subk	32,a2
	jrlt	avbkgoaround		;In front of me?
avbko1dok
	move	*a13(plyr_o2dist),a14
	cmpi	80,a14
	jrgt	avbko2dok			;He's too far?
	cmp	a14,a1
	jrlt	avbko2dok			;I'm closer?
	move	*a13(plyr_o2dir),a2
	sub	a0,a2
	abs	a2
	cmpi	040H,a2
	jrle	avbko2dsml
	subi	080H,a2
	abs	a2
avbko2dsml	subk	32,a2
	jrlt	avbkgoaround		;In front of me?
avbko2dok
	jruc	avbkrunath

avbkgoaround				;>Opponent in my way
	move	*a13(plyr_tmdist),a0
	cmpi	70,a0
	jrlt	avbkgoa			;Teammate too close?

	callr	drone_pass
	jrnz	avbkx			;Pass OK?

avbkgoa
	movi	drnzzcnt,a2
	move	*a2,a0
	subk	1,a0
	jrgt	avbkzzsame

	movk	3,a0			;New mode
	callr	rnd
	move	a0,*a2(drnzzmode-drnzzcnt)
	movi	TSEC-10,a0
	callr	rndrng0
	addk	10,a0
avbkzzsame
	move	a0,*a2

	callr	drone_seek
	sll	3,a0			;*8
	addi	avbkjbits_t,a0

	move	*a2(drnzzmode-drnzzcnt),a14
	sll	4+3,a14			;*16*8
	add	a14,a0
avbkmd1	movb	*a0,a0
	move	a0,*a11

	cmpi	80,a4
	jrlt	avbkshoot2			;Close enough for jam?

	jruc	avbktryshot


avbkrunath					;>I have a clr path to hoop!
	move	*a13(plyr_o1dist),a14
	cmpi	65,a14
	jrlt	avbkgoa			;He's too close?
	move	*a13(plyr_o2dist),a14
	cmpi	65,a14
	jrlt	avbkgoa			;He's too close?

	callr	drone_seek
	move	*a13(plyr_d_skill),a14
	cmpi	-11-3,a14
	jrle	avbktryshot
	ori	BUT3_M,a0		;Turbo
	move	a0,*a11


avbktryshot
	cmpi	50,a4
	jrlt	avbkshoot2			;Close enough for jam?

	move	@PSTATUS,a0		;Plyr start bits 0-3
	move	*a13(plyr_num),a1
	XORK	1,a1
	btst	a1,a0
	jrnz	avbkx			;Teammate is a human?

	callr	drone_chk3ptr
	jrnz	avbkshoot2			;Need a 3?

	movi	50,a0
	move	*a13(plyr_d_skill),a14
	cmpi	-8,a14
	jrgt	avbkrndsht
	movk	30,a0
avbkrndsht	callr	rndrng0
	move	a0,a0
	jrnz	avbkx

	cmpi	250,a4
	jrge	avbkx			;Too far?


avbkshoot2	move	*a11,a0			;>Shoot
	ori	BUT1_M|BUT3_M,a0
	move	a0,*a11
	jruc	avbkx



avbkfake					;>Jumping but still on gnd
	move	@PSTATUS,a0		;Plyr start bits 0-3
	move	*a13(plyr_num),a1
	XORK	1,a1
	btst	a1,a0
	jrnz	avbkx			;Teammate is a human?
	movk	01fH,a0
	callr	rnd
	jrnz	avbkx

	move	@game_time,a1,L
	cmpi	0200H,a1
	jrlt	avbkx			;Less than 2 secs?
	move	@shotimer+16,a1		;Tens
	jrnz	avbkfk
	move	@shotimer,a1		;Ones
	subk	2,a1
	jrlt	avbkx			;Less than 2 secs?
avbkfk
	jruc	avbkkilbuts


avbkinjmp
	move	@PSTATUS,a0		;Plyr start bits 0-3
	move	*a13(plyr_num),a1
	XORK	1,a1
	btst	a1,a0
	jrz	avbktmdrn			;Teammate is a drone?

	move	*a13(plyr_tmproc_p),a0,L
	move	*a0(PA11),a0,L
	move	*a0,a0			;Get teammates ctrl bits
	btst	BUT1_B,a0
	jrnz	avbkx			;Holding shoot button?
	jruc	avbkkilbuts
avbktmdrn
	move	*a3(plyr_seqflgs),a0
	btst	BLOCKREB_B,a0
	jrnz	avbkkilbuts		;Got a rebound?

	movk	15,a0
	callr	rndrng0
	move	a0,a0
	jrz	avbkkilbuts		;Cause shoot?

	movk	7,a0
	callr	rnd
	jrnz	avbkx			;88%?

	move	*a13(plyr_ptsdown),a14
	addk	5,a14
	jrlt	avbkpss			;Winning by 05H?
	move	*a13(plyr_seqflgs),a14
	btst	DUNK_B,a14
	jrnz	avbkx			;In a dunk?
	move	@game_time,a1,L
	cmpi	0200H,a1
	jrlt	avbkx			;Less than 2 secs?

avbkpss	callr	drone_pass

	jruc	avbkx



avbkkilbuts
	clr	a0			;>Let go of shoot button
	move	a0,*a11


avbkx	rets


avbkjbits_t
	.byte	0,JOYL_M,JOYR_M,0			;90~ clockwise
	.byte	JOYD_M,JOYD_M|JOYL_M,JOYD_M|JOYR_M,0
	.byte	JOYU_M,JOYU_M|JOYL_M,JOYU_M|JOYR_M,0
	.byte	0,0,0,0

	.byte	0,JOYR_M,JOYL_M,0			;90~ cntr clkwise
	.byte	JOYU_M,JOYU_M|JOYR_M,JOYU_M|JOYL_M,0
	.byte	JOYD_M,JOYD_M|JOYR_M,JOYD_M|JOYL_M,0
	.byte	0,0,0,0

	.byte	0,JOYD_M|JOYL_M,JOYU_M|JOYR_M,0		;135~ clkwise
	.byte	JOYD_M|JOYR_M,JOYD_M,JOYR_M,0
	.byte	JOYU_M|JOYL_M,JOYL_M,JOYU_M,0
	.byte	0,0,0,0

	.byte	0,JOYD_M|JOYR_M,JOYU_M|JOYL_M,0		;135~ cntr clkwise
	.byte	JOYU_M|JOYR_M,JOYR_M,JOYU_M,0
	.byte	JOYD_M|JOYL_M,JOYD_M,JOYL_M,0
	.byte	0,0,0,0



 SUBRP	drone_chk3ptr

	cmpi	290,a4
	jrgt	apnjx0			;Too far?

	move	*a13(plyr_num),a1
	move	@plyrnumonfire,a0
	cmp	a1,a0
	jreq	apnjx1			;I'm on fire?

	cmpi	230,a4
	jrlt	apnjx0			;Too close?

	move	@PSTATUS,a0		;Plyr start bits 0-3
	XORK	1,a1
	btst	a1,a0
	jrnz	apnjx0			;Teammate is a human?

	movk	6,a1
	move	@game_time,a0,L
	cmpi	01010000H,a0
	jrgt	apnjhvtime			;Enough time?
	movk	3,a1
apnjhvtime
	move	*a13(plyr_ptsdown),a14
	cmp	a1,a14
	jrlt	apnjx0

	cmpi	040000H,a0
	jrlt	apnjx1			;Less than 40 secs?

	move	*a13(plyr_o1dist),a14
	cmpi	70,a14
	jrlt	apnjrndsht			;He's close?
	move	*a13(plyr_o2dist),a14
	cmpi	70,a14
	jrge	apnjx1			;He's far?

apnjrndsht	movk	8,a0
	callr	rndrng0
	move	a0,a0
	jrnz	apnjx0

apnjx1	addk	1,a0
	rets

apnjx0	clr	a0
	rets



 SUBRP	drone_offwoball

	clr	a0
	move	a0,*a13(plyr_d_cflgs)


	move	*a13(plyr_d_mode),a14
	subk	1,a14
	jreq	pcjsinmd			;Already in mode?

	movk	1,a0			;Offense wo ball
	move	a0,*a13(plyr_d_mode)

	callr	drone_getcurskillo
	addi	pcjsmdsk_t,a0
	move	*a0,a0
	callr	rndrng0
	addk	1,a0
	move	a0,*a13(plyr_d_seekcnt)
pcjsinmd


	move	*a13(plyr_dir),a3

	move	*a13(plyr_o1dist),a14
	subi	50,a14
	jrgt	pcjso1far				;Too far?
	move	*a13(plyr_o1dir),a2
	sub	a3,a2
	abs	a2
	cmpi	040H,a2
	jrle	pcjso1dsml
	subi	080H,a2
	abs	a2
pcjso1dsml	subk	16,a2
	jrlt	pcjspusho				;In front of me?
pcjso1far
	move	*a13(plyr_o2dist),a14
	subi	50,a14
	jrgt	pcjsnopush				;Too far?
	move	*a13(plyr_o2dir),a2
	sub	a3,a2
	abs	a2
	cmpi	040H,a2
	jrle	pcjso2dsml
	subi	080H,a2
	abs	a2
pcjso2dsml	subk	16,a2
	jrge	pcjsnopush				;!In front?
pcjspusho
	movi	99,a0
	callr	rndrng0

	move	*a13(plyr_ptsdown),a14
	move	*a13(plyr_d_skill),a1
	add	a1,a14
	cmpi	15,a14
	jrle	pcjsmxdnok
	movk	15,a14
pcjsmxdnok	addk	15,a14
	jrge	pcjsdnok
	clr	a14
pcjsdnok	sll	4,a14
	addi	pcjsp_t,a14
	move	*a14,a1

	cmp	a1,a0
	jrge	pcjsnewseek			;Skip push?

	move	*a11,a0				;Push
	ori	BUT2_M<<8|BUT2_M|BUT3_M,a0
	move	a0,*a11
	jruc	pcjsx
pcjsnopush


	movi	07fH,a0
	callr	rnd
	jrz	pcjsnewseek

	move	*a13(plyr_d_seekcnt),a0
	subk	1,a0
	jrgt	pcjsseek

pcjsnewseek
	move	*a13(plyr_newdir),a0
	jrnn	pcjscontsk				;Turning?

	movk	16,a0
	callr	rndrng0
	sll	5,a0				;*32
	addi	pcjsseek_t,a0

	move	*a0+,a1
	move	*a13(plyr_num),a14
	subk	2,a14
	jrge	pcjslft
	neg	a1
pcjslft	addi	WRLDMID,a1
	move	a1,*a13(plyr_d_seekx)

	move	*a0+,a1
	move	a1,*a13(plyr_d_seeky)

	movi	TSEC*3,a0
	callr	rndrng0
	addk	TSEC/2,a0

pcjsseek	move	a0,*a13(plyr_d_seekcnt)
pcjscontsk
	callr	drone_seek
	jrnz	pcjsnotthere

	movk	01fH,a0				;3%
	callr	rnd
	jrnz	pcjsx
	clr	a0
	move	a0,*a13(plyr_d_seekcnt)

pcjsnotthere


pcjsx
	rets


pcjsmdsk_t					;Mode switch max seek time
	.word	50,50,50,50,50		;Up 15-11
	.word	40,40,40,35,30		;10-6
	.word	25,22,20,18,16		;5-1
	.word	14			;Even score
	.word	10,8,6,4,4		;Dn 1-5
	.word	4,3,3,3,2		;6-10
	.word	2,2,2,2,1		;11-15

pcjsp_t					;% to push
	.word	1,1,1,1,1
	.word	2,2,2,2,3
	.word	3,3,4,4,5
	.word	5
	.word	5,6,8,10,13
	.word	15,17,18,20,20
	.word	25,30,35,40,50


	.asg	CZMID,Z
pcjsseek_t
	.word	-320,Z-160, -250,Z-160, -120,Z-100	;3ptrs
	.word	-120,Z
	.word	-120,Z+100, -250,Z+160, -320,Z+160

	.word	-320,Z-100, -250,Z-80, -200,Z-50	;2ptrs
	.word	-180,Z
	.word	-200,Z+50, -250,Z+80, -320,Z+100

	.word	-310,Z-10, -300,Z, -310,Z+10



 SUBRP	drone_pass

	move	@PSTATUS,a0			;Plyr start bits 0-3
	move	*a13(plyr_num),a1
	XORK	1,a1
	btst	a1,a0
	jrnz	vkaxx				;Teammate is a human?

	move	*a13(plyr_tmdist),a0
	addk	10,a0

	move	*a13(plyr_o1dist),a1		;0CHhk if o1 in my way
	cmp	a1,a0
	jrlt	vkaxo1ok

	move	*a13(plyr_tmdir),a14
	move	*a13(plyr_o1dir),a1
	sub	a14,a1
	abs	a1
	cmpi	64,a1
	jrle	vkaxdsml
	subi	128,a1
	abs	a1
vkaxdsml	subk	16,a1
	jrlt	vkaxinway
vkaxo1ok

	move	*a13(plyr_o2dist),a1		;0CHhk if o2 in my way
	cmp	a1,a0
	jrlt	vkaxo2ok

	move	*a13(plyr_tmdir),a14
	move	*a13(plyr_o2dir),a1
	sub	a14,a1
	abs	a1
	cmpi	64,a1
	jrle	vkaxdsml2
	subi	128,a1
	abs	a1
vkaxdsml2	subk	16,a1
	jrlt	vkaxinway
vkaxo2ok

	move	*a13(plyr_tmproc_p),a1,L
vkaxtmclos
	move	*a1(plyr_seq),a0
	subk	RUNDRIBTURB_SEQ,a0
	jrhi	vkaxx				;Tm is doing something?

	move	*a11,a0				;>Pass
	ori	BUT2_M<<8|BUT2_M|BUT3_M,a0
	move	a0,*a11

	rets

vkaxinway
	move	*a13(plyr_ohpdist),a1
	cmpi	250,a1
	jrlt	vkaxx
	move	*a13(plyr_tmproc_p),a1,L
	move	*a1(plyr_ohpdist),a0
	cmpi	240,a0
	jrlt	vkaxtmclos				;Teammate is close to hoop?

vkaxx	clr	a0
	rets



 SUBRP	drone_defence

	PUSH	a7,a10


	move	@ballpnum,a5
	sll	5,a5
	addi	plyrproc_t,a5
	move	*a5,a5,L		;A5=*Proc of opponent with ball


	callr	drone_getcurskillo
	move	a0,a7			;A7=Ptsdn+skill for indexing (*16)


	clr	a0
	move	a0,*a13(plyr_d_cflgs)

	move	*a13(plyr_d_mode),a14
	jrn	lqkhinmd			;Already in mode?

	subk	1,a0			;-1
	move	a0,*a13(plyr_d_mode)

	move	a7,a14
	addi	lqkhmdsk_t,a14
	move	*a14,a0
	move	a0,a2
	srl	2,a2			;/4

	callr	rndrng0
	addk	1,a0
	add	a2,a0
	move	a0,*a13(plyr_d_seekcnt)

	clr	a0
	move	a0,*a9(pld_d_nastycnt)
lqkhinmd


	movk	2,a10			;>Update nasty mode
	move	@game_time,a14,L

	cmpi	0800H,a14
	jrlt	lqkhnasty			;Less than 8 secs?

	move	@gmqrtr,a2
	subk	3,a2
	jrlt	lqkhchkst
	move	*a13(plyr_ptsdown),a1
	addk	3,a1
	jrle	lqkhchkst			;Winning by 3 or more?
	cmpi	030000H,a14
	jrlt	lqkhnasty			;Less than 30 secs?

lqkhchkst	move	@shotimer+16,a14	;Tens
	jrnz	lqkhscok
	move	@shotimer,a14		;Ones
	subk	5,a14
	jrlt	lqkhnasty			;Less than 5 secs?

lqkhscok	move	*a9(pld_d_nastycnt),a10
	jrgt	lqkhnaston

	movi	999,a0
	callr	rndrng0

	move	a7,a14
	addi	lqkhnast_t,a14
	move	*a14,a1
	cmp	a1,a0
	jrge	lqkhnonast			;No nasty?

	movi	TSEC,a10

lqkhnaston	subk	1,a10
lqkhnasty	move	a10,*a9(pld_d_nastycnt)
lqkhnonast


	move	*a13(plyr_d_seekcnt),a0
	subk	1,a0
	jrgt	lqkhseek

	move	*a13(plyr_num),a2
	XORK	2,a2
	move	a2,a4
	sll	5,a4			;*32
	addi	plyrproc_t,a4
	move	*a4,a4,L
	cmp	a5,a4
	jreq	lqkhguard			;I'm on guy with ball?

	move	*a5(plyr_ohpdist),a0
	cmpi	300,a0
	jrgt	lqkhguard			;Too far to worry about?

	move	*a5(plyr_seqflgs),a0
	btst	DUNK_B,a0
	jrnz	lqkhgbc			;He's dunking?

	move	*a13(plyr_tmproc_p),a3,L
	move	*a3(plyr_seq),a0
	subi	STAGGER_SEQ,a0
	jrls	lqkhtmok
	subk	FLYBACKWB2_SEQ-STAGGER_SEQ,a0
	jrls	lqkhgbc			;Teammates staggered?
lqkhtmok
	move	*a3(plyr_o1dist),a14
	move	*a3(plyr_o1dir),a1
	btst	0,a2
	jrnz	lqkhp1
	move	*a3(plyr_o2dist),a14
	move	*a3(plyr_o2dir),a1
lqkhp1
	move	*a3(plyr_hpdir),a0	;Find dir difference
	sub	a1,a0
	abs	a0
	cmpi	040H,a0
	jrle	lqkhdsml
	subi	080H,a0
	abs	a0
lqkhdsml	subk	28,a0
	jrle	lqkhgbc			;TM not between op and hoop?

	cmpi	160,a14
	jrgt	lqkhgbc			;TM not guarding?
	jruc	lqkhguard

lqkhgbc	move	a5,a4			;Guard ball carrier
lqkhguard
	move	*a4(PA8),a2,L		;*Obj

	move	*a2(OXPOS),a0
	move	*a2(OXANI+16),a14
	add	a14,a0			;X
	move	*a2(OZPOS),a1		;Z

	move	*a2(OXVEL),a14,L
	sra	16-4,a14		;16 ticks from now
	add	a14,a0
	move	*a2(OZVEL),a14,L
	sra	16-4,a14		;16 ticks from now
	add	a14,a1

	move	a10,a10
	jrgt	lqkhsetseek		;Nasty on?

	move	*a5(plyr_seqflgs),a14
	btst	SHOOT_B,a14
	jrnz	lqkhsetseek		;Starting a shot?

	move	*a13(plyr_myhoopx),a2	;>Stay near oplyr between my basket
	movi	CZMID,a3

	add	a0,a2
	sll	1,a0
	add	a2,a0
	sra	2,a0			;/4

	add	a1,a3
	sll	1,a1
	add	a3,a1
	sra	2,a1			;/4
lqkhsetseek
	move	a0,*a13(plyr_d_seekx)
	move	a1,*a13(plyr_d_seeky)

	movk	25,a0
	move	*a5(plyr_hpdist),a1
	cmpi	320,a1
	jrge	lqkhseek			;Far from my hoop?


	move	a7,a14
	addi	lqkhskt_t,a14
	move	*a14,a0
	move	a0,a2
	srl	2,a2			;/4
	callr	rndrng0
	add	a2,a0

lqkhseek	move	a0,*a13(plyr_d_seekcnt)

	callr	drone_seek
	move	a0,a2



	move	*a13(plyr_num),a14	;>Get opponents proc
	addk	2,a14
	sll	32-2,a14
	srl	32-2-5,a14		;*32
	addi	plyrproc_t,a14
	move	*a14,a0,L
	move	*a0(plyr_ohpdist),a0
	subk	10,a0
	cmpi	300,a0
	jrlt	lqkhlong
	subi	80,a0			;Turbo earlier
lqkhlong
	move	*a13(plyr_hpdist),a1
	cmp	a0,a1
	jrlt	lqkhicloser
	addi	BUT3_M,a2		;Turbo
lqkhicloser


	move	*a13(plyr_balldist),a1
	cmpi	35,a1
	jrgt	lqkhpsrnd			;!In his face?

	move	a10,a10
	jrgt	lqkhps			;Nasty on?

lqkhpsrnd
	move	a7,a14
	addi	lqkhdist_t,a14
	move	*a14,a0
	cmp	a0,a1
	jrgt	lqkhskipsp			;Ball too far?

	movi	999,a0
	callr	rndrng0

	move	a7,a14
	addi	lqkhps_t,a14
	move	*a14,a1
	cmp	a1,a0
	jrge	lqkhskipsp			;Skip push?
lqkhps
	sll	32-4,a2
	srl	32-4,a2
	addk	BUT2_M,a2		;Steal

	movk	1,a0
	callr	rnd
	jrnz	lqkhskipsp			;50%?

	addi	BUT2_M<<8|BUT3_M,a2	;Push
lqkhskipsp


	move	*a13(plyr_balldist),a14	;0CHhk if I can block ball
	cmpi	65,a14
	jrge	lqkhnoblk

	move	*a5(plyr_jmpcnt),a0
	jrnz	lqkhtryblk			;Plyr with ball is in air?

	move	*a5(plyr_seqflgs),a0
	btst	SHOOT_B,a0
	jrz	lqkhnoblk			;!Starting a shot?
	movk	11,a0
	callr	rndrng0
	move	a0,a0
	jrnz	lqkhnoblk			;94% ignore?
	jruc	lqkhblk

lqkhtryblk
	move	a10,a10
	jrgt	lqkhblk			;Nasty on?

	movi	99,a0
	callr	rndrng0
	move	a7,a14
	addi	lqkhblk_t,a14
	move	*a14,a1
	cmp	a1,a0
	jrge	lqkhnoblk

lqkhblk
	sll	32-4,a2
	srl	32-4,a2
	addk	BUT1_M,a2		;Block
lqkhnoblk

	move	a2,*a11

	PULL	a7,a10
	rets


lqkhmdsk_t					;Mode switch max seek time
	.word	50,50,50,50,50		;Up 15-11
	.word	50,50,45,45,40		;10-6
	.word	33,25,22,18,16		;5-1
	.word	14			;Even score
	.word	10,8,6,4,4		;Dn 1-5
	.word	4,3,3,3,2		;6-10
	.word	2,2,2,2,1		;11-15

lqkhnast_t					;% to become nasty
	.word	0,0,0,0,0
	.word	1,1,2,2,2
	.word	3,3,3,4,6
	.word	8
	.word	10,11,12,13,14
	.word	15,17,20,25,26
	.word	27,28,29,30,35
lqkhskt_t					;Max seek time
	.word	60,60,58,55,52
	.word	48,44,40,36,32
	.word	28,24,22,18,16
	.word	15			;Even score
	.word	15,15,14,14,14
	.word	14,14,14,13,13
	.word	13,13,13,12,12

lqkhdist_t					;Max dist to try push/steal
	.word	110,100,100,100,100
	.word	90,90,80,80,80,80
	.word	70,70,60,60,60
	.word	50
	.word	50,50,50,50,50
	.word	50,50,50,50,50
	.word	50,50,50,50,50
lqkhps_t					;% to push/steal
	.word	1,2,2,2,2
	.word	3,3,3,3,3
	.word	4,4,4,5,5
	.word	6
	.word	6,6,8,10,13
	.word	15,17,18,20,30
	.word	40,60,80,150,250
lqkhblk_t					;% to block
	.word	1,1,2,3,3
	.word	3,3,4,4,5
	.word	6,7,8,10,12
	.word	14
	.word	16,18,20,25,30
	.word	35,40,45,50,50
	.word	50,50,50,50,50



 SUBR	drone2_setuptob

	PUSH	a2,a3,a4,a13

	movk	4,a4
	movi	plyrproc_t,a3

xtftlp	move	*a3+,a13,L
	move	*a13(PA9),a2,L

	movk	1,a0
	move	*a13(plyr_ownball),a14
	jrz	xtftdef			;Defense?
					;>Setup offense
	jrn	xtftwob
	movk	2,a0
xtftwob	move	a0,*a13(plyr_d_mode)

	movi	TSEC,a0
	callr	rndrng0
	addk	5,a0
	move	a0,*a13(plyr_d_seekcnt)

	movk	9-1,a0
	callr	rndrng0
	sll	5,a0			;*32
	addi	xtftseek_t,a0

	move	*a0+,a1
	move	*a13(plyr_num),a14
	subk	2,a14
	jrlt	xtftlft
	neg	a1
xtftlft	addi	WRLDMID,a1
	move	a1,*a13(plyr_d_seekx)

	move	*a0+,a1
	move	a1,*a13(plyr_d_seeky)

	jruc	xtftnxt


xtftdef					;>Setup defense
	movi	-1,a14
	move	a14,*a13(plyr_d_mode)	;Defense

	clr	a0
	move	a0,*a2(pld_d_nastycnt)

	callr	drone_getcurskillo

	addi	xtftmdsk_t,a0
	move	*a0,a0
	move	a0,a2
	srl	2,a2			;/4

	callr	rndrng0
	addk	1,a0
	add	a0,a2

	movk	1,a0
	callr	rnd
	jrnz	xtftsk
	srl	1,a2
xtftsk
	move	a2,*a13(plyr_d_seekcnt)

xtftnxt
	dsj	a4,xtftlp

	PULL	a2,a3,a4,a13
	rets


	.asg	CZMID,Z
xtftseek_t
	.word	-280,Z-160, -200,Z-160,	-100,Z-160, -50,Z-160
	.word	0,Z
	.word	-280,Z+160, -200,Z+160,	-100,Z+160, -50,Z+160


xtftmdsk_t					;Mode switch max seek time
	.word	50,50,50,50,50		;Up 15-11
	.word	50,50,45,45,40		;10-6
	.word	33,25,22,18,16		;5-1
	.word	14			;Even score
	.word	10,8,6,4,4		;Dn 1-5
	.word	4,3,3,3,2		;6-10
	.word	2,2,2,2,1		;11-15



 SUBRP	drone_chaseball

	move	*a13(plyr_rcvpass),a14
	jrgt	drone_offwoball		;I'm rcving ball?

	move	*a13(plyr_tmproc_p),a2,L
	move	*a2(plyr_rcvpass),a14
	jrgt	drone_offwoball		;Teammate rcving ball?


	clr	a0
	move	a0,*a13(plyr_d_cflgs)

	move	*a13(plyr_d_mode),a14
	jrz	hxhdinmd			;Already in mode?

	move	a0,*a13(plyr_d_mode)

	move	*a13(plyr_ptsdown),a14
	move	*a13(plyr_d_skill),a0
	add	a0,a14
	cmpi	15,a14
	jrle	hxhdmxdnok
	movk	15,a14
hxhdmxdnok	addk	15,a14
	jrge	hxhddnok
	clr	a14
hxhddnok	sll	4,a14
	addi	hxhdmdsk_t,a14
	move	*a14,a0
	callr	rndrng0
	addk	2,a0
	move	a0,*a13(plyr_d_seekcnt)

hxhdinmd

	move	*a13(plyr_d_seekcnt),a0
	subk	1,a0
	jrgt	hxhdseek

	move	*a2(plyr_seq),a0
	subi	STAGGER_SEQ,a0
	jrls	hxhdtmok
	subk	FLYBACKWB2_SEQ-STAGGER_SEQ,a0
	jrls	hxhdchaseb			;Teammates staggered?
hxhdtmok
	move	*a13(plyr_balldist),a0
	move	*a2(plyr_balldist),a14
	cmp	a14,a0
	jrlt	hxhdchaseb			;I'm closer?

	move	*a13(plyr_ohoopx),a0	;Go for opponents top of 3 pt line
	subi	WRLDMID,a0
	sra	2,a0			;/4
	addi	WRLDMID,a0
	movi	CZMID,a1
	jruc	hxhdsetxz

hxhdchaseb
	move	@ballobj_p,a5,L

	move	*a5(OXPOS),a0
	move	*a5(OXANI+16),a14
	add	a14,a0
	move	*a5(OXVEL),a14,L
	sra	16-3,a14		;8 ticks from now
	add	a14,a0

	move	*a5(OZPOS),a1
	move	*a5(OZVEL),a14,L
	sra	16-3,a14		;8 ticks from now
	add	a14,a1
hxhdsetxz
	move	a0,*a13(plyr_d_seekx)
	move	a1,*a13(plyr_d_seeky)


	move	*a13(plyr_ptsdown),a14
	move	*a13(plyr_d_skill),a0
	add	a0,a14
	cmpi	15,a14
	jrle	hxhdmxdnok2
	movk	15,a14
hxhdmxdnok2
	addk	15,a14
	jrge	hxhddnok2
	clr	a14
hxhddnok2	sll	4,a14
	addi	hxhdskt_t,a14
	move	*a14,a0

	callr	rndrng0
	addk	5,a0
hxhdseek	move	a0,*a13(plyr_d_seekcnt)

	callr	drone_seek


	move	*a13(plyr_balldist),a2	;0CHhk if I can jump at ball
	subk	30,a2
	jrge	hxhdnojmp

	move	*a5(OYPOS),a0
	addk	25,a0
	move	*a8(OYPOS),a1
	cmp	a1,a0
	jrlt	hxhdnojmp

	move	*a13(plyr_num),a0
	move	@plyrnumonfire,a1
	cmp	a0,a1
	jreq	hxhdonfire			;On fire?

	movk	7,a0
	callr	rnd
	jrnz	hxhdnojmp			;87%?
hxhdonfire
	move	@game_time,a1,L		;0DHon't take our own shots off the rim
	cmpi	0300H,a1
	jrgt	hxhdj			;More than 3 secs?

	move	*a13(plyr_num),a0
	move	@ballpnumshot,a1
	srl	1,a0
	srl	1,a1
	cmp	a0,a1
	jreq	hxhdnojmp			;My team?

hxhdj	move	*a11,a0			;Jmp
	ORK	BUT1_M,a14
	move	a0,*a11
hxhdnojmp

	rets

hxhdmdsk_t					;Mode switch max seek time
	.word	80,80,80,80,80		;Up 15-11
	.word	80,70,60,55,50		;10-6
	.word	33,25,22,18,16		;5-1
	.word	14			;Even score
	.word	10,8,6,4,4		;Dn 1-5
	.word	4,3,3,3,2		;6-10
	.word	2,2,2,2,1		;11-15

hxhdskt_t					;Max seek time
	.word	50,50,45,45,45
	.word	40,40,30,30,20
	.word	20,16,15,15,15
	.word	15
	.word	15,15,15,15,15
	.word	15,15,15,15,15
	.word	15,15,15,15,15



 SUBRP	drone_seek

	move	*a13(plyr_d_seekx),a0
	move	*a13(plyr_d_seeky),a1


 SUBRP	drone_seekxy

	move	a2,b0

	move	*a8(OXPOS),a2
	move	*a8(OXANI+16),a14
	add	a14,a2
	sub	a0,a2

	clr	a0

	move	a2,a14
	abs	a2
	subk	10,a2
	jrle	ivrnonx
	move	a14,a14
	jrlt	ivrnnolft
	subk	4,a0			;Left

ivrnnolft	addk	8,a0			;Rgt
ivrnonx
	move	*a8(OZPOS),a2

	sub	a1,a2
	move	a2,a14
	abs	a2
	subk	10,a2
	jrle	ivrnonz
	move	a14,a14
	jrlt	ivrnnoup
	subk	1,a0			;Up

ivrnnoup	addk	2,a0			;Dn
ivrnonz
	move	a0,*a11

	move	b0,a2
	move	a0,a0
	rets



 SUBRP	drone_getcurskillo

	move	*a13(plyr_ptsdown),a0
	move	*a13(plyr_d_skill),a14
	add	a14,a0
	cmpi	15,a0
	jrle	tvtjmxdnok
	movk	15,a0
tvtjmxdnok	addk	15,a0
	jrge	tvtjdnok
	clr	a0
tvtjdnok	sll	4,a0			;A7=Ptsdn+skill for indexing (*16)

	rets




 SUBR	drone2_adjskill

	PUSH	a2,a3,a4,a5,a6

	move	a0,a5
	subk	2,a5
	abs	a5
	move	@gmqrtr,a1
	cmpi	3,a1
	jrls	jscnqok
	movk	3,a1			;Overtime
jscnqok	movk	3,a0
	mpyu	a0,a1
	add	a1,a5			;A5=Quarter+minute index (0-11)


	movk	ADJDIFF,a0		;Get difficulty level
	calla	GET_ADJ			;Drone v2 is level 4 & 5
	subk	4,a0			;-2 to 2
	move	a0,a6
	sll	1,a0			;*2
	add	a0,a6			;A6=Difficulty adj (0 or 3)

	movi	plyrproc_t,a4
	movk	4,b0
jscnlp
	move	*a4+,a3,L

	move	*a3(plyr_d_skill),a2

	move	*a3(plyr_ptsdown),a14
	cmpi	15,a14
	jrle	jscnmxdnok
	movk	15,a14
jscnmxdnok	addk	15,a14
	jrge	jscndnok
	clr	a14
jscndnok	sll	4,a14
	addi	jscnadj_t,a14
	move	*a14,a14
	add	a14,a2

	move	a5,a14			;0CHhk quarter minimum
	sll	3,a14
	addi	jscnmin_t,a14
	movb	*a14,a14
	add	a6,a14
	cmp	a14,a2
	jrge	jscnminok
	move	a14,a2
jscnminok
	move	*a3(plyr_num),a1
	XORK	1,a1
	move	@PSTATUS,a14
	btst	a1,a14
	jrnz	jscntminok			;Teammate is human?

	move	@team1,a1		;0CHhk team minimum
	cmpi	3,b0
	jrge	jscnt1
	move	@team2,a1
jscnt1	movk	12,a0
	mpyu	a0,a1

	add	a5,a1
	sll	3,a1			;*8
	addi	jscntdmin_t,a1
	movb	*a1,a14
	add	a6,a14
	cmp	a14,a2
	jrge	jscntminok
	move	a14,a2
jscntminok
	move	a2,*a3(plyr_d_skill)

	dsj	b0,jscnlp

	PULL	a2,a3,a4,a5,a6
	rets


jscnadj_t	.word	-5,-5,-5,-5,-5
	.word	-5,-5,-5,-4,-3
	.word	-2,-1,0,0,0
	.word	0
	.word	1,1,2,2,3
	.word	3,4,4,5,5
	.word	5,6,6,6,7

jscnmin_t	.byte	-15,-12,-10, -8,-7,-6, -5,-4,-4, -3,-2,-3

TMDIFF	.macro
	.byte	-10,-9,-8, -7,-6,-5, -4,-4,-3, -2,0,-2
	.endm
jscntdmin_t
	TMDIFF						;ATL
	TMDIFF						;BOST
	TMDIFF						;CHAR
	.byte	6,7,8, 9,10,11, 12,12,13, 14,15,14	;CHI (1)
	.byte	-6,-5,-5, -4,-3,-3, -2,-2,-2, -1,0,-1	;CLEV (10)
	TMDIFF						;DAL
	TMDIFF						;DEN
	.byte	-5,-4,-4, -3,-2,-2, -1,-1,-1, 0,1,0	;DET (9)
	TMDIFF						;GOLD
	.byte	-4,-3,-3, -2,-1,-1, 0,0,0, 1,2,1	;HOU (8)
	.byte	-3,-2,-2, -1,0,0, 1,1,1, 2,3,2		;IND (7)
	TMDIFF						;LAC
	.byte	-1,-1,0, 1,1,1, 2,2,2, 3,4,3		;LAL (6)
	TMDIFF						;MIAMI
	TMDIFF						;MILW
	TMDIFF						;MINN
	TMDIFF						;NJ
	.byte	1,1,2, 3,3,3, 4,4,4, 5,6,5		;NY (4)
	TMDIFF						;ORL
	TMDIFF						;PHIL
	.byte	4,4,5, 6,6,6, 7,7,7, 8,9,8		;PHX (2)
	.byte	2,2,3, 4,4,4, 5,5,5, 6,7,6		;PORT (3)
	TMDIFF						;SAC
	TMDIFF						;SANANT
	TMDIFF						;SEA
	.byte	0,0,1, 2,2,2, 3,3,3, 4,5,4		;UTAH (5)
	TMDIFF						;WASH
	.even



 SUBRP	rnd

	move	@RAND,a1,L
	rl	a1,a1
	move	@HCOUNT,a14
	rl	a14,a1
	add	sp,a1
	move	a1,@RAND,L

	and	a1,a0
	rets



 SUBRP	rndrng0

	move	@RAND,a1,L
	rl	a1,a1
	move	@HCOUNT,a14
	rl	a14,a1
	add	sp,a1
	move	a1,@RAND,L

	addk	1,a0
	mpyu	a1,a0		;Condition codes not valid!

	rets




	.end

